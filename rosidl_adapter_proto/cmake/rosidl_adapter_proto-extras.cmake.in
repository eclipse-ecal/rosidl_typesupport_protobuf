# generated from
# rosidl_adapter_proto/
#   rosidl_adapter_proto-extras.cmake.in

find_package(Protobuf REQUIRED)

if(NOT Protobuf_FOUND)
  message(STATUS "Could not find Protobuf - skip rosidl_adapter_proto")
else()
  find_package(ament_cmake_core QUIET REQUIRED)
  ament_register_extension(
    "rosidl_generate_idl_interfaces"
    "rosidl_adapter_proto"
    "${rosidl_adapter_proto_DIR}/rosidl_adapt_proto_interfaces.cmake")

  set(rosidl_adapter_proto_BIN
    "${rosidl_adapter_proto_DIR}/../../../lib/rosidl_adapter_proto/rosidl_adapter_proto")
  normalize_path(rosidl_adapter_proto_BIN
    "${rosidl_adapter_proto_BIN}")

  set(rosidl_adapter_proto_GENERATOR_FILES
    "${rosidl_adapter_proto_DIR}/../../../@PYTHON_INSTALL_DIR@/rosidl_adapter_proto/__init__.py")
  normalize_path(rosidl_adapter_proto_GENERATOR_FILES
    "${rosidl_adapter_proto_GENERATOR_FILES}")

  set(rosidl_adapter_proto_TEMPLATE_DIR
    "${rosidl_adapter_proto_DIR}/../resource")
  normalize_path(rosidl_adapter_proto_TEMPLATE_DIR
    "${rosidl_adapter_proto_TEMPLATE_DIR}")

  include("${rosidl_adapter_proto_DIR}/rosidl_adapt_proto_interfaces.cmake")

  add_compile_definitions("ROSIDL_ADAPTER_PROTO_BUILDING_DLL__${PROJECT_NAME}")

  # There is no clean way to include additional files into protobuf headers
  if(NOT WIN32)
    add_compile_options("-include${rosidl_adapter_proto_VISIBILITY_CONTROL_HEADER}")
  else()
    add_compile_options("/FI\"${rosidl_adapter_proto_VISIBILITY_CONTROL_HEADER}\"")
  endif()

  foreach(_pkg_name ${rosidl_generate_interfaces_DEPENDENCY_PACKAGE_NAMES})
    set(_proto_dir "${${_pkg_name}_DIR}/../../../include/${_pkg_name}/${_pkg_name}/rosidl_adapter_proto__visibility_control.h")
    normalize_path(_proto_dir "${_proto_dir}")
    if(NOT WIN32)
      add_compile_options("-include${_proto_dir}")
    else()
      add_compile_options("/FI\"${_proto_dir}\"")
    endif()
  endforeach()

endif()
